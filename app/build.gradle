buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
        jcenter()
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
        classpath 'com.dicedmelon.gradle:jacoco-android:0.1.2'
    }
}
apply plugin: "com.android.application"
apply plugin: "io.fabric"
apply plugin: "jacoco-android"
// Fixes error when updating to new gradle
// apply plugin: 'com.jakewharton.butterknife'
dependencies {
    implementation 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'
}

ext {
    supportLibraryVersion = "27.0.2"
    ormLiteVersion = "4.46"
    powermockVersion = "1.7.3"
    dexmakerVersion = "1.2"
}


repositories {
    maven { url 'https://maven.fabric.io/public' }
}

def generateGitBuild = { ->

    StringBuilder stringBuilder = new StringBuilder()
    stringBuilder.append('"')
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--always'
            standardOutput = stdout
        }
        String commitObject = stdout.toString().trim()
        stringBuilder.append(commitObject)
    } catch (ignored) {
        stringBuilder.append('NoGitSystemAvailable')
    }
    stringBuilder.append('-')
    stringBuilder.append((new Date()).format('yyyy.MM.dd'))
    stringBuilder.append('"')
    return stringBuilder.toString()
}

android {
    compileSdkVersion 27
    buildToolsVersion '28.0.3'

    defaultConfig {
        applicationId "info.nightscout.androidaps"
        minSdkVersion 21
        targetSdkVersion 26
        multiDexEnabled true
        versionCode 1500
        version "1.58"
        buildConfigField "String", "VERSION", '"' + version + '"'
        buildConfigField "String", "BUILDVERSION", generateGitBuild()

        ndk {
            moduleName "BleCommandUtil"
        }
    }
    lintOptions {
        // TODO remove once wear dependency com.google.android.gms:play-services-wearable:7.3.0
        // has been upgraded (requiring significant code changes), which currently fails release
        // build with a deprecation warning
        //abortOnError false
        // (disabled entirely to avoid reports on the error, which would still be displayed
        //  and it's easy to overlook that it's ignored)
        checkReleaseBuilds false
        disable 'MissingTranslation'
        disable 'ExtraTranslation'
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            testCoverageEnabled true
        }
    }
    productFlavors {
        flavorDimensions "standard"
        full {
            dimension "standard"
            resValue "string", "app_name", "AndroidAPS"
            versionName version
            manifestPlaceholders = [
                    appIcon: "@mipmap/blueowl"
            ]
            buildConfigField "boolean", "APS", "true"
            buildConfigField "boolean", "PUMPDRIVERS", "true"
            buildConfigField "boolean", "NSCLIENTOLNY", "false"
            buildConfigField "boolean", "CLOSEDLOOP", "true"
            buildConfigField "boolean", "G5UPLOADER", "false"
            buildConfigField "boolean", "PUMPCONTROL", "false"
        }
        openloop {
            dimension "standard"
            resValue "string", "app_name", "AndroidAPS"
            versionName version
            manifestPlaceholders = [
                    appIcon: "@mipmap/blueowl"
            ]
            buildConfigField "boolean", "APS", "true"
            buildConfigField "boolean", "PUMPDRIVERS", "true"
            buildConfigField "boolean", "NSCLIENTOLNY", "false"
            buildConfigField "boolean", "CLOSEDLOOP", "false"
            buildConfigField "boolean", "G5UPLOADER", "false"
            buildConfigField "boolean", "PUMPCONTROL", "false"
        }
        pumpcontrol {
            dimension "standard"
            resValue "string", "app_name", "AndroidAPS"
            versionName version
            manifestPlaceholders = [
                    appIcon: "@mipmap/blueowl"
            ]
            buildConfigField "boolean", "APS", "false"
            buildConfigField "boolean", "PUMPDRIVERS", "true"
            buildConfigField "boolean", "NSCLIENTOLNY", "false"
            buildConfigField "boolean", "CLOSEDLOOP", "false"
            buildConfigField "boolean", "G5UPLOADER", "false"
            buildConfigField "boolean", "PUMPCONTROL", "true"
        }
        nsclient {
            dimension "standard"
            resValue "string", "app_name", "NSClient"
            versionName version + "-nsclient"
            manifestPlaceholders = [
                    appIcon: "@mipmap/yellowowl"
            ]
            buildConfigField "boolean", "APS", "false"
            buildConfigField "boolean", "PUMPDRIVERS", "false"
            buildConfigField "boolean", "NSCLIENTOLNY", "true"
            buildConfigField "boolean", "CLOSEDLOOP", "false"
            buildConfigField "boolean", "G5UPLOADER", "false"
            buildConfigField "boolean", "PUMPCONTROL", "false"
        }
        g5uploader {
            dimension "standard"
            resValue "string", "app_name", "NSClient"
            versionName version + "-nsclient"
            manifestPlaceholders = [
                    appIcon: "@mipmap/yellowowl"
            ]
            buildConfigField "boolean", "APS", "false"
            buildConfigField "boolean", "PUMPDRIVERS", "false"
            buildConfigField "boolean", "NSCLIENTOLNY", "false"
            buildConfigField "boolean", "CLOSEDLOOP", "false"
            buildConfigField "boolean", "G5UPLOADER", "true"
            buildConfigField "boolean", "PUMPCONTROL", "false"
        }
    }
}

allprojects {
    repositories {
        jcenter()
        flatDir {
            dirs 'libs'
        }
    }
}
// Updated to meet new criteria for adding dependencies
// Replaced compile with implementation
// Replaced testCompile with testImplementation
// Replaced androidTestCompile with androidTestImplementation
dependencies {
    implementation 'com.android.support.constraint:constraint-layout:1.1.2'
    wearApp project(':wear')

    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation("com.crashlytics.sdk.android:crashlytics:2.6.7@aar") {
        transitive = true
    }
    implementation("com.crashlytics.sdk.android:answers:1.3.12@aar") {
        transitive = true
    }

    implementation "com.android.support:appcompat-v7:${supportLibraryVersion}"
    implementation "com.android.support:support-v4:${supportLibraryVersion}"
    implementation "com.android.support:cardview-v7:${supportLibraryVersion}"
    implementation "com.android.support:recyclerview-v7:${supportLibraryVersion}"
    implementation "com.android.support:gridlayout-v7:${supportLibraryVersion}"
    implementation "com.android.support:design:${supportLibraryVersion}"
    implementation "com.android.support:percent:${supportLibraryVersion}"
    implementation "com.wdullaer:materialdatetimepicker:2.3.0"
    implementation "com.squareup:otto:1.3.7"
    implementation "com.j256.ormlite:ormlite-core:${ormLiteVersion}"
    implementation "com.j256.ormlite:ormlite-android:${ormLiteVersion}"
    implementation("com.github.tony19:logback-android-classic:1.1.1-6") {
        exclude group: "com.google.android", module: "android"
    }
    implementation "org.apache.commons:commons-lang3:3.6"
    implementation "org.slf4j:slf4j-api:1.7.12"
    implementation "com.jjoe64:graphview:4.0.1"
    implementation "com.joanzapata.iconify:android-iconify-fontawesome:2.1.1"
    implementation "com.google.android.gms:play-services-wearable:7.5.0"
    implementation(name: "android-edittext-validator-v1.3.4-mod", ext: "aar")
    implementation("com.google.android:flexbox:0.3.0") {
        exclude group: "com.android.support"
    }
    implementation("io.socket:socket.io-client:0.8.3") {
        // excluding org.json which is provided by Android
        exclude group: "org.json", module: "json"
    }
    implementation "com.google.code.gson:gson:2.7"
    implementation "com.google.guava:guava:20.0"

    implementation "net.danlew:android.joda:2.9.9.1"

    api "com.jakewharton:butterknife:8.8.1"
    annotationProcessor "com.jakewharton:butterknife-compiler:8.8.1"

    testImplementation "junit:junit:4.12"
    testImplementation "org.json:json:20140107"
    testImplementation "org.mockito:mockito-core:2.7.22"
    testImplementation "org.powermock:powermock-api-mockito2:${powermockVersion}"
    testImplementation "org.powermock:powermock-module-junit4-rule-agent:${powermockVersion}"
    testImplementation "org.powermock:powermock-module-junit4-rule:${powermockVersion}"
    testImplementation "org.powermock:powermock-module-junit4:${powermockVersion}"
    testImplementation "joda-time:joda-time:2.9.4.2"

    androidTestImplementation "org.mockito:mockito-core:2.7.22"
    androidTestImplementation "com.google.dexmaker:dexmaker:${dexmakerVersion}"
    androidTestImplementation "com.google.dexmaker:dexmaker-mockito:${dexmakerVersion}"
}
