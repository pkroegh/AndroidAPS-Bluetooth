apply plugin: 'com.android.application'

ext {
    supportLibraryVersion = "25.2.0"
    wearableVersion = "2.0.1"    
}

def generateGitBuild = { ->

    StringBuilder stringBuilder = new StringBuilder()
    stringBuilder.append('"')
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--always'
            standardOutput = stdout
        }
        String commitObject = stdout.toString().trim()
        stringBuilder.append(commitObject)
    } catch (ignored) {
        stringBuilder.append('NoGitSystemAvailable')
    }
    stringBuilder.append('-')
    stringBuilder.append((new Date()).format('yyyy.MM.dd'))
    stringBuilder.append('"')
    return stringBuilder.toString()
}

android {
    compileSdkVersion 27
    buildToolsVersion '28.0.3'

    defaultConfig {
        applicationId "info.nightscout.androidaps"
        minSdkVersion 20
        targetSdkVersion 26
        versionCode 1
        versionName "1.0.2"
        buildConfigField "String", "BUILDVERSION", generateGitBuild()
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    publishNonDefault true

}

allprojects {
    repositories {
        jcenter()
        flatDir {
            dirs 'libs'
        }
    }
}

// Updated to meet new criteria for adding dependencies
// Replaced compile with implementation
dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation files("libs/hellocharts-library-1.5.5.jar")
    //compile "com.ustwo.android:clockwise-wearable:1.0.2"
    compileOnly "com.google.android.wearable:wearable:${wearableVersion}"
    implementation "com.google.android.support:wearable:${wearableVersion}"
    implementation "com.google.android.gms:play-services-wearable:7.3.0"
    implementation(name:"ustwo-clockwise-debug", ext:"aar")
    implementation "com.android.support:support-v4:${supportLibraryVersion}"
    implementation "me.denley.wearpreferenceactivity:wearpreferenceactivity:0.5.0"
}